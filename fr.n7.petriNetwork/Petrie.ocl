import 'petrie.ecore'

package petrie

context PetriElement
def: petrinet(): PetriNet =
	PetriNet.allInstances()
		->select(n | n.petriElements -> includes(self))
		->asSequence()->first()
	
context PetriNet
inv validPetriNetName('Invalid PetriNet name: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')
	
context Arc
inv validArcPosition('All arcs should be between Place and Transition'):
	(self.source.oclIsKindOf(Place)
	 and self.target.oclIsKindOf(Transition))
	 or (self.target.oclIsKindOf(Place)
	 and self.source.oclIsKindOf(Transition))

context ArcSimple
inv hasStrictPositiveWeight('Regular arc weight should be strictly positive'):
	self.weight > 0
	
	
context Arc
inv sourceAndTargetInSamePetriNet('Arc source and target should belong to the same PetriNet than the Arc'):
	self.source.petrinet() = self.petrinet() and self.target.petrinet() = self.petrinet()

context ArcRead
inv arcFromTransitionIsNotReadArc('A arc from a Transition cannot be a read-only arc'):
	not (self.source.oclIsKindOf(Transition))
	
context Place
inv hasPositiveMarking('Place marking should be positive: ' + self.name):
	self.marking >= 0
	
context Node
inv validNodeName('Invalid Node name: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')
	
context Node
inv uniqueNodeName('Node name ' + self.name + ' already exists'):
	self.petrinet().petriElements
		->select(e | e.oclIsKindOf(Node))
		->collect(e | e.oclAsType(Node))
		->select(e | self.name = e.name)
		->size() = 1
	
context Arc
inv noRedundantNeed('Two Arcs with the same source and the same target should not exist'):
	self.petrinet().petriElements
	->select(e | e.oclIsKindOf(Arc))
	->collect(e | e.oclAsType(Arc))
	->select(e | self.source = e.source and self.target = e.target)
	->size() = 1
	
context Transition
inv timeIsStriclyPositive('Time interval in Transition should be positive'):
	self.min >= 0 and self.max >= 0
	
inv timeDeltaIsStriclyPositive('Time delta interval between min and max in Transition should be positive'):
	self.max - self.min >= 0
		
endpackage